apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

//===============================================//
//                                               //
//                   Android                     //
//                                               //
//===============================================//
android {
    // compile-sdk-version
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'com.godoer.geotodo'
        testApplicationId 'com.godoer.geotodo.test'
        // target-sdk-version
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        // app-version
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            renderscriptOptimLevel 3
            minifyEnabled true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            renderscriptOptimLevel 3
            minifyEnabled false
            versionNameSuffix '_debug'
            zipAlignEnabled true
        }
    }

    // This is important, it will run lint checks but won't abort build
    lintOptions {
        abortOnError false
    }

    productFlavors {}

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    defaultConfig.applicationId + "-" +
                            buildType.name + "-v" +
                            defaultConfig.versionName + "-" +
                            defaultConfig.versionCode + ".apk" );
        }
    }
}

//===============================================//
//                                               //
//                    Signing                    //
//                                               //
//===============================================//
def Properties props = new Properties()
def propFile = file('signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null &&
            props.containsKey('STORE_FILE') &&
            props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') &&
            props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

//===============================================//
//                                               //
//                 Repositories                  //
//                                               //
//===============================================//
repositories {
    maven {
        url "https://mint.splunk.com/gradle/"
    }
}

//===============================================//
//                                               //
//                 Dependencies                  //
//                                               //
//===============================================//
dependencies {
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:support-v4:22.1.1'
    // MAP
    compile project(':MapController_v1.15.7')
    // cards-lib
    compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'
    compile 'com.github.gabrielemariotti.cards:cardslib-core:2.0.1'
    compile 'com.github.gabrielemariotti.cards:cardslib-recyclerview:2.0.1'
    compile 'com.github.gabrielemariotti.cards:cardslib-cards:2.0.1'
    // fab
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.getbase:floatingactionbutton:1.9.0'
    // progress-fragment
    compile 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0'
    // splunk ACRA
    compile 'com.splunk.mint:mint:4.1'
}